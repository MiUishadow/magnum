# Author: mosra <mosra@centrum.cz>
pkgname=nacl-magnum
pkgver=dev.newlib
pkgrel=1
pkgdesc="C++11/C++14 and OpenGL 2D/3D graphics engine (NaCl newlib version)"
arch=('any')
url="http://mosra.cz/blog/magnum.php"
license=('MIT')
depends=('nacl-corrade')
makedepends=('nacl-sdk' 'cmake' 'corrade' 'ninja')
options=('!strip' '!buildflags')

_rootdir=$startdir/../../

build() {
    # Build 32bit
    mkdir -p "$_rootdir/build-nacl-newlib-x86-32"
    cd "$_rootdir/build-nacl-newlib-x86-32"

    cmake .. \
        -DCMAKE_TOOLCHAIN_FILE="$_rootdir/toolchains/generic/NaCl-newlib-x86-32.cmake" \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr/nacl \
        -DWITH_MAGNUMINFO=ON \
        -DWITH_NACLAPPLICATION=ON \
        -DWITH_WINDOWLESSNACLAPPLICATION=ON \
        -DWITH_MAGNUMFONT=ON \
        -DWITH_MAGNUMFONTCONVERTER=ON \
        -DWITH_OBJIMPORTER=ON \
        -DWITH_TGAIMAGECONVERTER=ON \
        -DWITH_TGAIMPORTER=ON \
        -DWITH_WAVAUDIOIMPORTER=ON \
        -DLIB_SUFFIX=/32 \
        -G Ninja
    ninja

    # Build 64bit
    mkdir -p "$_rootdir/build-nacl-newlib-x86-64"
    cd "$_rootdir/build-nacl-newlib-x86-64"

    cmake .. \
        -DCMAKE_TOOLCHAIN_FILE="$_rootdir/toolchains/generic/NaCl-newlib-x86-64.cmake" \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr/nacl \
        -DWITH_MAGNUMINFO=ON \
        -DWITH_NACLAPPLICATION=ON \
        -DWITH_WINDOWLESSNACLAPPLICATION=ON \
        -DWITH_MAGNUMFONT=ON \
        -DWITH_MAGNUMFONTCONVERTER=ON \
        -DWITH_OBJIMPORTER=ON \
        -DWITH_TGAIMAGECONVERTER=ON \
        -DWITH_TGAIMPORTER=ON \
        -DWITH_WAVAUDIOIMPORTER=ON \
        -G Ninja
    ninja
}

package() {
    # Install 32bit
    cd "$_rootdir/build-nacl-newlib-x86-32"
    DESTDIR="$pkgdir/" ninja install/strip

    # Install 64bit (the headers will be overwritten, but they are (and should
    # be) the same for both versions
    cd "$_rootdir/build-nacl-newlib-x86-64"
    DESTDIR="$pkgdir/" ninja install/strip
}
